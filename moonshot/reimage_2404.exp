#!/usr/bin/expect --

proc named {args defaults} {
    upvar 1 "" ""
    array set "" $defaults
    foreach {key value} $args {
        if {![info exists ($key)]} {
            error "bad option '$key', should be one of: [lsort [array names {}]]"
        }
        set ($key) $value
    }
}
named $argv {--chassis "" --node "" --boot-params ""}

set chassis $(--chassis)
set node $(--node)
set boot_params $(--boot-params)

send_user "\rConnecting to $chassis to control $node ..."

set force_conservative 1
if {$force_conservative} {
	set send_slow {1 .1}
	proc send {ignore arg} {
		sleep .1
		exp_send -s -- $arg
	}
}

set timeout 120
spawn ssh -oKexAlgorithms=+diffie-hellman-group1-sha1 -oCiphers=+aes128-cbc -oStrictHostKeyChecking=no "$chassis"
match_max 100000
sleep 5
expect {
    "hpiLO->" {}
    eof { send_user "\rConnection closed before hpiLO prompt\n"; exit 1 }
    timeout { send_user "\rTimed out waiting for hpiLO prompt\n"; exit 1 }
}
send -- "set node bootonce pxe $node\r"
expect "bootonce: PXE"
expect "hpiLO->"
set retries 2
while { $retries > 0 } {
	send -- "set node power off force $node\r"
	expect "hpiLO->"
	set waiting 6
	send_user "\rWaiting for power off "
	while { $waiting > 0 } {
	    sleep 5
	    set waiting [ expr $waiting - 1 ]
	    send -- "show node power $node\r"
	    expect {
		"*Off" { set waiting -1 }
		"*On" { send_user "." }
	    }
	}
    if { $waiting == -1 } { set retries 0 }
    set retries [ expr $retries - 1 ]
}
send_user "\rPower is off"
send_user "\n\r"
sleep 0.5
set retries 2
while { $retries > 0 } {
	send -- "set node power on $node\r"
	expect "hpiLO->"
	set waiting 6
	send_user "\rWaiting for power on "
	while { $waiting > 0 } {
	    sleep 5
	    set waiting [ expr $waiting - 1 ]
	    send -- "show node power $node\r"
	    expect {
		"*On" { set waiting -1 }
		"*Off" { send_user "." }
	    }
	}
    if { $waiting == -1 } { set retries 0 }
    set retries [ expr $retries - 1 ]
}
send_user "\rPower is on"
send_user "\n\r"
sleep 0.2
expect "hpiLO->"
send -- "connect node vsp $node\r"
send_user "\rcheck vsp\r"
expect "Virtual Serial Port Active: "
send_user "\n\r"
set timeout -1

# netboot.xyz: select the 24.04 automated install
send_user "\rWaiting for netboot.xyz menu on $node "
expect "netboot.xyz"
expect "booting default..."
sleep 5
# press down key 3 times and then press enter
exp_send -- "\033\[B"
sleep 1
exp_send -- "\033\[B"
sleep 1
exp_send -- "\033\[B"
sleep 1
send -- "\r"
sleep 10

# exit virtual serial port
#  `Press 'ESC (' to return to the CLI Session.`
exp_send -- "\033("
expect "hpiLO->"
send -- "exit\r"
expect eof
send_user "\rFinished $chassis $node"
